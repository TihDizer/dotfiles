{ inputs, ... }:
{
  flake-file.inputs = {
    yazi = {
      url = "github:sxyazi/yazi";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  flake.modules.homeManager.yazi =
    {
      pkgs,
      config,
      lib,
      ...
    }:
    let
      yazi-wrapper = pkgs.writeShellScriptBin "yazi-wrapper" ''
        set -e

        multiple="$1"
        directory="$2"
        save="$3"
        path="$4"
        out="$5"

        if [ "$save" = "1" ]; then
          set -- --chooser-file="$out" "$path"
        elif [ "$directory" = "1" ]; then
          set -- --chooser-file="$out" --cwd-file="$out" "$path"
        else
          set -- --chooser-file="$out" "$path"
        fi

        exec ${lib.getExe pkgs.kitty} \
          --class=file_chooser \
          -e ${lib.getExe config.programs.yazi.package} \
          "$@"
      '';

      yazi-floating = pkgs.writeShellScriptBin "yazi-floating" ''
        exec ${lib.getExe pkgs.kitty} \
          --class=yazi-floating \
          -e ${lib.getExe config.programs.yazi.package} \
          "$@"
      '';
    in
    {
      programs.yazi = {
        enable = true;
        package = inputs.yazi.packages.${pkgs.stdenv.hostPlatform.system}.default;
        enableBashIntegration = true;
        enableZshIntegration = true;
        enableFishIntegration = true;

        plugins = {
          mount = pkgs.yaziPlugins.mount;
          smart-enter = pkgs.yaziPlugins.smart-enter;
          ouch = pkgs.yaziPlugins.ouch;
          piper = pkgs.yaziPlugins.piper;
          git = {
            package = pkgs.yaziPlugins.git;
            setup = true;
          };
          lazygit = pkgs.yaziPlugins.lazygit;
          open-git-remote = pkgs.yaziPlugins.mkYaziPlugin {
            pname = "open-git-remote";
            version = "0-unstable-2026-05-12";
            src = pkgs.fetchFromGitHub {
              owner = "larry-oates";
              repo = "open-git-remote.yazi";
              rev = "72158d607c01b63bc4eb5ac6fc0dc0691b41ce8c";
              hash = "sha256-DGiGn5NveIGU0BikQ9U3vPL2R/qo1VA9ZfElOwl6qPk=";
            };
          };
          omni-trash = pkgs.yaziPlugins.mkYaziPlugin {
            pname = "omni-trash";
            version = "0-unstable-2026-09-20";
            src = pkgs.fetchFromGitHub {
              owner = "goon";
              repo = "omni-trash.yazi";
              rev = "3c2a9923673e0552a093afc4122473df1d427a93";
              hash = "sha256-heqqEWzJCoNt3CIJAEaWfqUX4J9BfVEw3OsU7Xjc17M=";
            };
          };
          copy-file-contents = {
            package = pkgs.yaziPlugins.mkYaziPlugin {
              pname = "copy-file-contents";
              version = "0-unstable-2026-08-07";
              src = pkgs.fetchFromGitHub {
                owner = "AnirudhG07";
                repo = "plugins-yazi";
                rev = "3f4f1a3ea58707ce87b6455ebc25e7954b261e43";
                hash = "sha256-hNw+1BRVHPR1LgUE6MYtnJEAO4fhSI3m+3M8wzw53UQ=";
              };
              installPhase = ''
                runHook preInstall
                cp -r copy-file-contents.yazi $out
                runHook postInstall
              '';
            };
            setup = true;
            settings = {
              append_char = "\n";
              notification = true;
            };
          };
        };

        theme = {
          git = {
            unknown_sign = "";
            clean_sign = "";
            unstaged_sign = "~";
            staged_sign = "+";
            added_sign = "+";
            deleted_sign = "-";
            untracked_sign = "?";
            ignored_sign = "*";
            updated_sign = "=";
          };
        };

        settings = {
          opener = {
            edit = [
              {
                run = "nvim %s";
                desc = "Neovim";
                block = true;
              }
            ];
            play = [
              {
                run = "mpv %s";
                desc = "MPV";
                orphan = true;
              }
              {
                run = "mediainfo %s1; echo 'Press enter to exit'; read _";
                desc = "Show media info";
                block = true;
              }
            ];
            office = [
              {
                run = "onlyoffice-desktopeditors %s";
                desc = "ONLYOFFICE";
                orphan = true;
              }
            ];
            readest = [
              {
                run = "readest %s";
                desc = "Readest";
                orphan = true;
              }
            ];
            imv = [
              {
                run = "imv %s";
                desc = "imv";
                orphan = true;
              }
            ];
            chrome = [
              {
                run = "google-chrome-stable %s";
                desc = "Google Chrome";
                orphan = true;
              }
            ];
            firefox = [
              {
                run = "firefox %s";
                desc = "Firefox";
                orphan = true;
              }
            ];
            glow = [
              {
                run = "glow -p -w 0 %s";
                desc = "Glow";
                block = true;
              }
            ];
          };

          open = {
            prepend_rules = [
              {
                url = "*.{zip,rar,7z,7z.*,tar,tgz,tbz2,txz,gz,xz,zst,bz2}";
                use = [ "extract" "reveal" ];
              }
              {
                mime = "application/{vnd.openxmlformats-officedocument.*,msword,vnd.ms-*,vnd.oasis.opendocument.*,x-msword}";
                use = [ "office" "open" "reveal" ];
              }
              {
                url = "*.{doc,docx,odt,rtf,xls,xlsx,ods,ppt,pptx,odp}";
                use = [ "office" "open" "reveal" ];
              }
              {
                mime = "application/pdf";
                use = [ "readest" "chrome" "firefox" "open" "reveal" ];
              }
              {
                url = "*.pdf";
                use = [ "readest" "chrome" "firefox" "open" "reveal" ];
              }
              {
                mime = "text/markdown";
                use = [ "edit" "glow" "open" "reveal" ];
              }
              {
                url = "*.{md,markdown,mdown,mkd}";
                use = [ "edit" "glow" "open" "reveal" ];
              }
              {
                mime = "image/*";
                use = [ "imv" "chrome" "open" "reveal" ];
              }
              {
                mime = "{audio,video}/*";
                use = [ "play" "reveal" ];
              }
              {
                mime = "text/html";
                use = [ "chrome" "firefox" "edit" "open" "reveal" ];
              }
              {
                url = "*.{html,htm}";
                use = [ "chrome" "firefox" "edit" "open" "reveal" ];
              }
              {
                mime = "application/{json,ndjson,javascript,wine-extension-ini,xml,toml,yaml,x-yaml}";
                use = [ "edit" "chrome" "reveal" ];
              }
              {
                mime = "text/*";
                use = [ "edit" "glow" "open" "reveal" ];
              }
            ];
          };

          plugin = {
            prepend_fetchers = [
              {
                url = "*";
                run = "git";
                group = "git";
              }
              {
                url = "*/";
                run = "git";
                group = "git";
              }
            ];

            prepend_previewers = [
              {
                url = "*.{zip,rar,7z,7z.*,tar,tgz,tbz2,txz,gz,xz,zst,bz2}";
                run = "archive";
              }
              {
                url = "*.csv";
                run = ''piper -- mlr --icsv --opprint --barred-unicode --right-align-numeric --ragged --lazy-quotes -C cat "$1"'';
              }
              {
                mime = "text/csv";
                run = ''piper -- mlr --icsv --opprint --barred-unicode --right-align-numeric --ragged --lazy-quotes -C cat "$1"'';
              }
              {
                url = "*.tsv";
                run = ''piper -- mlr --itsv --opprint --barred-unicode --right-align-numeric --ragged --lazy-quotes -C cat "$1"'';
              }
              {
                mime = "text/tab-separated-values";
                run = ''piper -- mlr --itsv --opprint --barred-unicode --right-align-numeric --ragged --lazy-quotes -C cat "$1"'';
              }
              {
                url = "*.html";
                run = ''piper -- w3m -dump -T text/html -cols "$w" "$1"'';
              }
              {
                url = "*.htm";
                run = ''piper -- w3m -dump -T text/html -cols "$w" "$1"'';
              }
              {
                mime = "text/html";
                run = ''piper -- w3m -dump -T text/html -cols "$w" "$1"'';
              }
              {
                url = "*.docx";
                run = ''piper -- pandoc --quiet -s -t plain --columns="$w" "$1"'';
              }
              {
                mime = "application/vnd.openxmlformats-officedocument.wordprocessingml.document";
                run = ''piper -- pandoc --quiet -s -t plain --columns="$w" "$1"'';
              }
              {
                url = "*.doc";
                run = ''piper -- catdoc -d utf-8 -m "$w" "$1"'';
              }
              {
                mime = "application/msword";
                run = ''piper -- catdoc -d utf-8 -m "$w" "$1"'';
              }
              {
                url = "*.odt";
                run = ''piper -- pandoc --quiet -s -t plain --columns="$w" "$1"'';
              }
              {
                mime = "application/vnd.oasis.opendocument.text";
                run = ''piper -- pandoc --quiet -s -t plain --columns="$w" "$1"'';
              }
              {
                url = "*.rtf";
                run = ''piper -- pandoc --quiet -s -t plain --columns="$w" "$1"'';
              }
              {
                mime = "application/rtf";
                run = ''piper -- pandoc --quiet -s -t plain --columns="$w" "$1"'';
              }
            ];
          };
        };

        keymap = {
          mgr.prepend_keymap = [
            {
              on = "l";
              run = "plugin smart-enter";
              desc = "Enter the child directory, or open the file";
            }
            {
              on = "M";
              run = "plugin mount";
              desc = "Mount device / disk";
            }
            {
              on = [ "g" "i" ];
              run = "plugin lazygit";
              desc = "Run lazygit";
            }
            {
              on = [ "g" "l" ];
              run = "plugin open-git-remote";
              desc = "Open git remote URL in browser";
            }
            {
              on = [ "g" "t" ];
              run = "plugin omni-trash";
              desc = "Open trash manager";
            }
            {
              on = "<A-y>";
              run = "plugin copy-file-contents -- plain";
              desc = "Copy contents of file(s)";
            }
            {
              on = "<A-Y>";
              run = "plugin copy-file-contents -- multi";
              desc = "Copy contents of file(s) with filename + code fence";
            }
            {
              on = [ "g" "c" ];
              run = "cd ${config.home.homeDirectory}/dotfiles";
              desc = "Go to ~/dotfiles";
            }
            {
              on = [ "g" "d" ];
              run = "cd ${config.xdg.userDirs.download}";
              desc = "Go to downloads";
            }
            {
              on = "y";
              run = [
                ''shell -- for path in %s; do echo "file://$path"; done | wl-copy -t text/uri-list''
                "yank"
              ];
            }
            {
              on = "x";
              run = [
                ''shell -- for path in %s; do echo "file://$path"; done | wl-copy -t text/uri-list''
                "yank --cut"
              ];
            }
            {
              on = "<C-p>";
              run = ''shell -- wl-paste -t text/uri-list 2>/dev/null | tr -d "\r" | while IFS= read -r uri; do [ -z "$uri" ] && continue; if [[ "$uri" =~ ^file://(.*) ]]; then path="''${BASH_REMATCH[1]}"; printf -v path '%b' "''${path//%/\\x}"; else path="$uri"; fi; [ -e "$path" ] && cp -r -- "$path" .; done'';
            }
          ];
        };
      };

      home.sessionVariables = {
        GTK_USE_PORTAL = "1";
        QT_QPA_PLATFORMTHEME = lib.mkForce "xdgdesktopportal";
        TDESKTOP_USE_GTK_FILE_DIALOG = "1";
      };

      xdg.desktopEntries.yazi = {
        name = "Yazi";
        genericName = "File Manager";
        comment = "Terminal file manager";

        exec =
          "${yazi-floating}/bin/yazi-floating %U";

        icon = "yazi";

        terminal = false;
        type = "Application";

        categories = [
          "System"
          "FileManager"
          "FileTools"
        ];

        mimeType = [
          "inode/directory"
          "application/x-directory"
        ];
      };

      xdg.mimeApps = {
        enable = true;
        defaultApplications = {
          "inode/directory" = [ "yazi.desktop" ];
          "application/x-directory" = [ "yazi.desktop" ];
        };
      };

      xdg.portal = {
        extraPortals = [ pkgs.xdg-desktop-portal-termfilechooser ];
        config = {
          common = {
            "org.freedesktop.impl.portal.FileChooser" = [ "termfilechooser" ];
          };
          niri = {
            "org.freedesktop.impl.portal.FileChooser" = [ "termfilechooser" ];
          };
        };
      };

      xdg.configFile."xdg-desktop-portal-termfilechooser/config" = {
        text = ''
          [filechooser]
          cmd=${yazi-wrapper}/bin/yazi-wrapper
          default_dir=$HOME
        '';
      };

      home.packages = with pkgs; [
        glow # Terminal markdown viewer / pager
        miller # Fast tabular data processor (CSV, TSV, JSON)
        lazygit # Terminal UI for git
        _7zz # 7-Zip archiver (required by yazi for extraction)
        trash-cli # Trash manager
        ouch # Painless compression and decompression in the terminal
        w3m # Text-based web browser / HTML renderer
        pandoc # Document converter (docx, odt, etc.)
        catdoc # MS-Word (.doc) text extractor
        util-linux # Provides lsblk, eject for mount plugin
        ffmpeg # Multimedia framework
        poppler # PDF rendering library
        fd # Fast find alternative
        file # File type detector
        jq # JSON processor
        ripgrep # Fast grep (rg)
        fzf # Fuzzy finder
        zoxide # Smart cd (z)
        resvg # SVG rasterizer
        imagemagick # Image manipulation
        bat # Cat clone with syntax highlighting
        lsd # Modern ls alternative
        ripdrag # Drag and drop utility
        wl-clipboard-rs # Wayland clipboard
      ];
    };
}
